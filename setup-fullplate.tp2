BACKUP ~fullplate/backup~
AUTHOR ~erik, gibberlings3~
MODDER
VERSION ~alfa 5a~
NO_IF_EVAL_BUG

BEGIN ~Full Plate And Packing Steel: Clean, Shine and Oil all Armour (naming and enchantment level fixes, required for main component)~
 DESIGNATED 101
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  PATCH_IF (SOURCE_SIZE < 0x72) BEGIN
    PATCH_PRINT "%SOURCE_FILE%: Invalid, below minimum size"
  END ELSE BEGIN
    READ_BYTE  0x18 "flags"

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~BAND01.ITM~) BEGIN
      WRITE_LONG 0x54 0xffffffff // non-magical, doesn't need identified_desc
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~BAND02.ITM~) BEGIN
      WRITE_LONG 0x34 2400 // too cheap for +1
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~BAND04.ITM~) BEGIN
      SAY NAME2 ~Eternal Age +2~
      WRITE_BYTE 0x60 2
      WRITE_LONG 0x34 7000 // same price as standard +2 banded, too cheap
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~BLEAT01.ITM~) BEGIN
      WRITE_BYTE 0x60 1
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~BLEAT02.ITM~) BEGIN
      WRITE_BYTE 0x18 ("%flags%" BAND 0b10111111)
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~BLEAT03.ITM~) BEGIN
      WRITE_BYTE 0x18 ("%flags%" BAND 0b10111111)
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~BRUENPLA.ITM~) BEGIN
      SAY NAME1 ~Field Plate Mail~
      WRITE_BYTE 0x18 ("%flags%" BOR 0b01000000)
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~C2CHAN01.ITM~) BEGIN
      SAY NAME2 ~Incarnadine Elven Chain +5~
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~C2KELD01.ITM~) BEGIN
      SAY NAME1 ~Firecam Full Plate Mail~
      SAY NAME2 ~Firecam Full Plate Mail +3~
      WRITE_BYTE 0x60 3
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~C2LEAT01.ITM~) BEGIN
      SAY NAME2 ~Human Flesh +8~
      WRITE_BYTE 0x60 8
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~C2PLAT01.ITM~) BEGIN
      SAY NAME1 ~Ankheg Plate Mail~
      SAY NAME2 ~Ankheg Plate Mail +4~
      WRITE_BYTE 0x18 ("%flags%" BOR 0b01000000)
      WRITE_BYTE 0x60 4
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~C2ROBE01.ITM~) BEGIN
      WRITE_BYTE 0x60 7
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~C2ROBE02.ITM~) BEGIN
      WRITE_BYTE 0x60 7
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~C2ROBE03.ITM~) BEGIN
      WRITE_BYTE 0x60 7
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~C2VALY01.ITM~) BEGIN
      SAY NAME1 ~Corthala Family Armor~
      SAY NAME2 ~Corthala Family Armor +5~
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~CHAN02.ITM~) BEGIN
      WRITE_BYTE 0x60 1
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~CHAN03.ITM~) BEGIN
      WRITE_BYTE 0x60 2
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~CHAN05.ITM~) BEGIN
      WRITE_BYTE 0x60 1
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~CHAN06.ITM~) BEGIN
      WRITE_BYTE 0x60 4
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~CHAN07.ITM~) BEGIN
      WRITE_BYTE 0x60 3
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~CHAN08.ITM~) BEGIN
      WRITE_BYTE 0x60 2
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~CHAN20.ITM~) BEGIN
      WRITE_BYTE 0x60 6
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~CHAN21.ITM~) BEGIN
      WRITE_BYTE 0x60 3
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~CLCK15.ITM~) BEGIN
      WRITE_BYTE 0x60 5
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~CLCK16.ITM~) BEGIN
      WRITE_BYTE 0x60 5
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~CLCK17.ITM~) BEGIN
      WRITE_BYTE 0x60 5
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~D0T#GORI.ITM~) BEGIN
      WRITE_BYTE 0x60 5
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~D0T#LEAT.ITM~) BEGIN
      WRITE_BYTE 0x60 3
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~DLEAT10.ITM~) BEGIN
      WRITE_BYTE 0x18 ("%flags%" BOR 0b01000000)
      WRITE_BYTE 0x60 2
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~DWCHAN01.ITM~) BEGIN
      SAY NAME2 ~Drow Adamantine Chain +3~
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~DWPLAT01.ITM~) BEGIN
      SAY NAME2 ~Drow Adamantine Full Plate +5~
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~FINIROBE.ITM~) BEGIN
      WRITE_BYTE 0x60 5
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~FLAT01.ITM~) BEGIN
      WRITE_LONG 0x54 0xffffffff // non-magical, doesn't need identified_desc
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~FLAT02.ITM~) BEGIN
      WRITE_LONG 0x34 9000 // too cheap for +1
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~FLAT04.ITM~) BEGIN
      SAY NAME2 ~Dwarven Field Plate +1~
      WRITE_BYTE 0x60 1
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~J#SPLI06.ITM~) BEGIN
      WRITE_BYTE 0x60 2
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~LEAT02.ITM~) BEGIN
      WRITE_BYTE 0x60 1
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~LEAT03.ITM~) BEGIN
      WRITE_BYTE 0x60 2
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~LEAT05.ITM~) BEGIN
      WRITE_BYTE 0x60 1
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~LEAT06.ITM~) BEGIN
      WRITE_BYTE 0x60 2
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~LEAT07.ITM~) BEGIN
      WRITE_BYTE 0x60 2
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~LEAT08.ITM~) BEGIN
      WRITE_BYTE 0x60 3
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~LEAT09.ITM~) BEGIN
      WRITE_BYTE 0x60 3
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~LEAT11.ITM~) BEGIN
      WRITE_BYTE 0x60 2
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~LEAT15.ITM~) BEGIN
      WRITE_BYTE 0x60 2
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~MGILEAT1.ITM~) BEGIN
      SAY NAME1 ~Leather Armor~
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~MGIPLAT1.ITM~) BEGIN
      SAY NAME1 ~Full Plate Mail~
      WRITE_BYTE 0x60 1
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~NPARM.ITM~) BEGIN
      WRITE_BYTE 0x60 1
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~NPCHAN.ITM~) BEGIN
      SAY NAME1 ~Corthala Family Armor~
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~NPPLAT.ITM~) BEGIN
      SAY NAME1 ~Firecam Full Plate Mail~
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~PLAT02.ITM~) BEGIN
      WRITE_BYTE 0x60 1
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~PLAT05.ITM~) BEGIN
      WRITE_BYTE 0x60 1
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~PLAT06.ITM~) BEGIN
      SAY NAME1 ~Ankheg Plate Mail~
      WRITE_BYTE 0x18 ("%flags%" BOR 0b01000000)
      WRITE_BYTE 0x60 2
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~PLAT08.ITM~) BEGIN
      WRITE_BYTE 0x60 3
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~PLAT09.ITM~) BEGIN
      SAY NAME1 ~Field Plate Mail~
      WRITE_BYTE 0x60 2
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~PLAT10.ITM~) BEGIN
      WRITE_BYTE 0x60 1
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~PLAT11.ITM~) BEGIN
      WRITE_BYTE 0x60 2
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~PLAT14.ITM~) BEGIN
      WRITE_BYTE 0x60 1
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~PLAT18.ITM~) BEGIN
      WRITE_BYTE 0x60 2
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~R!KITPLT.ITM~) BEGIN
      SAY NAME1 ~Plate Mail~
      SAY NAME2 ~Bringer of Vengeance: Kitanya's Plate Mail +4~
      WRITE_BYTE 0x60 4
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~RR#ABIH.ITM~) BEGIN
      SAY NAME2 ~Abishai Hide +3~
      WRITE_BYTE 0x60 3
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~RR#CHN01.ITM~) BEGIN
      SAY NAME2 ~Drow Chain Mail +1~
      WRITE_BYTE 0x60 1
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~SCAL01.ITM~) BEGIN
      WRITE_LONG 0x54 0xffffffff // non-magical, doesn't need identified_desc
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~SCAL02.ITM~) BEGIN
      WRITE_LONG 0x34 1600 // too cheap for +1
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~SHARARM.ITM~) BEGIN
      WRITE_BYTE 0x60 9
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~TAMCHAIN.ITM~) BEGIN
      WRITE_BYTE 0x60 5
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~TG#BSP2.ITM~) BEGIN
      WRITE_BYTE 0x60 2
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~TTLEAT.ITM~) BEGIN
      WRITE_BYTE 0x60 2
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~U#CHAN07.ITM~) BEGIN
      SAY NAME1 ~Chain Mail~
      SAY NAME2 ~Elven Chain of the Hand +3~
      WRITE_BYTE 0x60 3
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~U#HFHEAH.ITM~) BEGIN
      SAY NAME2 ~Cornugan Hide Armor +1~
      WRITE_BYTE 0x60 1
    END

    PATCH_IF ("%SOURCE_FILE%" STRING_EQUAL ~WA2ROBE.ITM~) BEGIN
      WRITE_BYTE 0x60 5
    END
  END
  BUT_ONLY_IF_IT_CHANGES


BEGIN ~Full Plate And Packing Steel: Between You And Harm (alternate armour system)~
 DESIGNATED 1
 REQUIRE_COMPONENT ~setup-fullplate.tp2~ ~101~ ~Oil the armour first, please.~

ACTION_IF FILE_EXISTS ~override/tb#resisttweak.txt~ THEN BEGIN
    PRINT ~WARNING: tb#resisttweak.txt found in override. Not recommended, may cause stutter.~
END

// define local pseudomacros

<<<<<<<< delitmfx.tpp
DELETE_BYTES %fxoff% + 0x30 * %x% + 0  0x30
SET fxnum = %fxnum% - 1
SET x = %x% - 1
>>>>>>>>

<<<<<<<< additmfx.tpp
INSERT_BYTES    %fxoff% + 0x30 * %fxnum%        0x30
WRITE_SHORT     %fxoff% + 0x30 * %fxnum% + 0x00 %opcode%
WRITE_BYTE      %fxoff% + 0x30 * %fxnum% + 0x02 1           // target self
WRITE_BYTE      %fxoff% + 0x30 * %fxnum% + 0x03 0           // power
WRITE_LONG      %fxoff% + 0x30 * %fxnum% + 0x04 %param1%
WRITE_LONG      %fxoff% + 0x30 * %fxnum% + 0x08 %param2%
WRITE_BYTE      %fxoff% + 0x30 * %fxnum% + 0x0c 2           // while equipped
WRITE_BYTE      %fxoff% + 0x30 * %fxnum% + 0x0d 0           // nonmagical
WRITE_LONG      %fxoff% + 0x30 * %fxnum% + 0x0e 0           // duration, n/a
WRITE_BYTE      %fxoff% + 0x30 * %fxnum% + 0x12 100         // probability upper
WRITE_BYTE      %fxoff% + 0x30 * %fxnum% + 0x13 0           // probability lower ... so this is 0-100 of a 0-100 range.
WRITE_LONG      %fxoff% + 0x30 * %fxnum% + 0x14 0           // resref dw 1, n/a
WRITE_LONG      %fxoff% + 0x30 * %fxnum% + 0x18 0           // resref dw 2, n/a
WRITE_LONG      %fxoff% + 0x30 * %fxnum% + 0x1c 0           // dice thrown, n/a
WRITE_LONG      %fxoff% + 0x30 * %fxnum% + 0x20 0           // dice sides, n/a
WRITE_LONG      %fxoff% + 0x30 * %fxnum% + 0x24 0           // save type, n/a
WRITE_LONG      %fxoff% + 0x30 * %fxnum% + 0x28 0           // save bonus, n/a
WRITE_LONG      %fxoff% + 0x30 * %fxnum% + 0x2c 0           // unknown, n/a
SET fxnum = %fxnum% + 1
>>>>>>>>

// takes intopcode, intparam, returns returnval, clobbers fxnum fxoff opcode param1 param2 i.
<<<<<<<< sumfx.tpp
READ_SHORT 0x70 fxnum
READ_LONG 0x6a fxoff
SET returnval = 0

FOR (i=0; (i < %fxnum%); i+=1) BEGIN
    READ_SHORT %fxoff% + 0x30 * %i% + 0     opcode
    READ_SLONG  %fxoff% + 0x30 * %i% + 0x04  param1
    READ_SLONG  %fxoff% + 0x30 * %i% + 0x08  param2

    PATCH_IF ( %opcode% = %intopcode% ) BEGIN
      PATCH_IF ( %intparam% = 1 ) BEGIN
        SET returnval = %returnval% + %param1%
      END
      PATCH_IF ( %intparam% = 2 ) BEGIN
        SET returnval = %returnval% + %param2%
      END
    END
END
>>>>>>>>

<<<<<<<< descupd_magic.tpp
PATCH_IF (enchantment > 0) BEGIN
   PATCH_IF ( ~%iddesc%~ STRING_CONTAINS_REGEXP ~%searchstring%~ == 0) BEGIN
     INNER_PATCH_SAVE newdescstr "%iddesc%" BEGIN
       REPLACE_EVALUATE ~%replacestring%~
         BEGIN
           SPRINT result ~%insertstring%~
         END
         ~%result%~
     END
   END ELSE BEGIN
     INNER_PATCH_SAVE newdescstr "%iddesc%" BEGIN
       REPLACE_EVALUATE ~%insertafter%~
         BEGIN
           SPRINT result  ~%MATCH1%%insertstring%~
           PATCH_IF (~%insertstring%~ STRING_EQUAL ~~ == 0) BEGIN
             SPRINT result  ~%result%%wnl%~
           END
         END
         ~%result%~
     END
   END
   SAY_EVALUATED IDENTIFIED_DESC ~%newdescstr%~
   READ_STRREF 0x54 iddesc ELSE ~nope~
END ELSE BEGIN // if enchanted
   PATCH_IF ( ~%uniddesc%~ STRING_CONTAINS_REGEXP ~%searchstring%~ == 0) BEGIN
     INNER_PATCH_SAVE newdescstr "%uniddesc%" BEGIN
       REPLACE_EVALUATE ~%replacestring%~
         BEGIN
           SPRINT result ~%insertstring%~
         END
         ~%result%~
     END
   END ELSE BEGIN
     INNER_PATCH_SAVE newdescstr "%uniddesc%" BEGIN
       REPLACE_EVALUATE ~%insertafter%~
         BEGIN
           SPRINT result  ~%MATCH1%%insertstring%~
           PATCH_IF (~%insertstring%~ STRING_EQUAL ~~ == 0) BEGIN
             SPRINT result  ~%result%%wnl%~
           END
         END
         ~%result%~
     END
   END
   SAY_EVALUATED UNIDENTIFIED_DESC ~%newdescstr%~
   READ_STRREF 0x50 uniddesc ELSE ~nope~
END // if not enchanted
>>>>>>>>

// read data files

COPY_EXISTING - ~fullplate/classification.txt~ ~fullplate~
    COUNT_2DA_ROWS 0 ~entrycount~
    FOR (i=0;i<~%entrycount%~;i=i+1) BEGIN
        READ_2DA_ENTRY ~%i%~ 0 0 ~name~
        READ_2DA_ENTRY ~%i%~ 1 0 ~class~
        TO_LOWER name
        TO_LOWER class
        SPRINT $class(~%name%~) ~%class%~
    END
    BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING - ~fullplate/thebiglist.txt~ ~fullplate~
    COUNT_2DA_ROWS 0 ~entrycount~
    FOR (i=0;i<~%entrycount%~;i=i+1) BEGIN
        READ_2DA_ENTRY ~%i%~ 0 0 ~name~
        READ_2DA_ENTRY ~%i%~ 1 0 ~_weight~
        READ_2DA_ENTRY ~%i%~ 2 0 ~_dexmod~
        READ_2DA_ENTRY ~%i%~ 3 0 ~_movemod~
        READ_2DA_ENTRY ~%i%~ 4 0 ~_wpnspeedmod~
        READ_2DA_ENTRY ~%i%~ 5 0 ~_baseac~
        READ_2DA_ENTRY ~%i%~ 6 0 ~_slashac~
        READ_2DA_ENTRY ~%i%~ 7 0 ~_crushac~
        READ_2DA_ENTRY ~%i%~ 8 0 ~_pierceac~
        READ_2DA_ENTRY ~%i%~ 9 0 ~_missileac~ /* really only here for the missile attractor */
        READ_2DA_ENTRY ~%i%~ 10 0 ~_slashres~
        READ_2DA_ENTRY ~%i%~ 11 0 ~_crushres~
        READ_2DA_ENTRY ~%i%~ 12 0 ~_pierceres~
        READ_2DA_ENTRY ~%i%~ 13 0 ~_missileres~
        READ_2DA_ENTRY ~%i%~ 14 0 ~_fireres~
        READ_2DA_ENTRY ~%i%~ 15 0 ~_coldres~
        READ_2DA_ENTRY ~%i%~ 16 0 ~_hidemod~
        READ_2DA_ENTRY ~%i%~ 17 0 ~_movesilmod~
        READ_2DA_ENTRY ~%i%~ 18 0 ~_openlockmod~
        READ_2DA_ENTRY ~%i%~ 19 0 ~_disarmtrapmod~
        READ_2DA_ENTRY ~%i%~ 20 0 ~_pickpocketmod~
        READ_2DA_ENTRY ~%i%~ 21 0 ~_spellfailmod~

        TO_LOWER name

        SPRINT $weight(~%name%~)        ~%_weight%~
        SPRINT $dexmod(~%name%~)        ~%_dexmod%~
        SPRINT $movemod(~%name%~)       ~%_movemod%~
        SPRINT $wpnspeedmod(~%name%~)   ~%_wpnspeedmod%~
        SPRINT $baseac(~%name%~)        ~%_baseac%~
        SPRINT $slashac(~%name%~)       ~%_slashac%~
        SPRINT $crushac(~%name%~)       ~%_crushac%~
        SPRINT $pierceac(~%name%~)      ~%_pierceac%~
        SPRINT $missileac(~%name%~)     ~%_missileac%~
        SPRINT $slashres(~%name%~)      ~%_slashres%~
        SPRINT $crushres(~%name%~)      ~%_crushres%~
        SPRINT $pierceres(~%name%~)     ~%_pierceres%~
        SPRINT $missileres(~%name%~)    ~%_missileres%~
        SPRINT $fireres(~%name%~)       ~%_fireres%~
        SPRINT $coldres(~%name%~)       ~%_coldres%~
        SPRINT $hidemod(~%name%~)       ~%_hidemod%~
        SPRINT $movesilmod(~%name%~)    ~%_movesilmod%~
        SPRINT $openlockmod(~%name%~)   ~%_openlockmod%~
        SPRINT $disarmtrapmod(~%name%~) ~%_disarmtrapmod%~
        SPRINT $pickpocketmod(~%name%~) ~%_pickpocketmod%~
        SPRINT $spellfailmod(~%name%~)  ~%_spellfailmod%~
    END
    BUT_ONLY_IF_IT_CHANGES

// allows us to regexp match tabs and newlines
INCLUDE ~fullplate/extra_regexp_vars.tph~

//////////////////////////////////////
// profit! and take over the world! //
//////////////////////////////////////
PRINT ~Reworking items and updating descriptions...~

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~

  PATCH_IF (SOURCE_SIZE < 0x72) BEGIN
    PATCH_PRINT "%SOURCE_FILE%: Invalid, below minimum size"
  END ELSE BEGIN
    // small workaround: reduce DEX requirements, so npcs still can
    //  use their custom equipment with (very) heavy armour.
    READ_BYTE  0x2c min_dex
    SET min_dex = %min_dex% - 6
    PATCH_IF min_dex < 0 BEGIN SET min_dex = 0 END
	WRITE_BYTE 0x2c %min_dex%

    READ_LONG 0x60 enchantment

    SPRINT usename ~~
    TO_LOWER SOURCE_RES

    // armor type defined for the item in classifications.txt?
    //  (we can guess based on animation type, if not)
    SPRINT classdefined $class(~%SOURCE_RES%~)
    PATCH_IF NOT (~%classdefined%~ STRING_CONTAINS_REGEXP ~class~ == 0) BEGIN
        SPRINT tempname ~%classdefined%%enchantment%~
        
        // stats listed in thebiglist.txt for the generic armor type?
        SPRINT classenchant $weight(~%tempname%~)
        PATCH_IF NOT (~%classenchant%~ STRING_CONTAINS_REGEXP ~weight~ == 0) BEGIN
            SPRINT usename ~%tempname%~
        END
    END
    
    PATCH_IF NOT ( ~%usename%~ STRING_EQUAL ~~ ) BEGIN
        // adjustments listed in thebiglist.txt for the specific armor?
        SPRINT specialcase $weight(~%SOURCE_RES%~)
        PATCH_IF NOT (~%specialcase%~ STRING_CONTAINS_REGEXP ~weight~ == 0) BEGIN
            SET $weight(~%SOURCE_RES%~)         = $weight(~%SOURCE_RES%~) + $weight(~%usename%~)
            SET $dexmod(~%SOURCE_RES%~)         = $dexmod(~%SOURCE_RES%~) + $dexmod(~%usename%~)
            SET $movemod(~%SOURCE_RES%~)        = $movemod(~%SOURCE_RES%~) + $movemod(~%usename%~)
            SET $wpnspeedmod(~%SOURCE_RES%~)    = $wpnspeedmod(~%SOURCE_RES%~) + $wpnspeedmod(~%usename%~)
            SET $baseac(~%SOURCE_RES%~)         = $baseac(~%SOURCE_RES%~) + $baseac(~%usename%~)
            SET $slashac(~%SOURCE_RES%~)        = $slashac(~%SOURCE_RES%~) + $slashac(~%usename%~)
            SET $crushac(~%SOURCE_RES%~)        = $crushac(~%SOURCE_RES%~) + $crushac(~%usename%~)
            SET $pierceac(~%SOURCE_RES%~)       = $pierceac(~%SOURCE_RES%~) + $pierceac(~%usename%~)
            SET $missileac(~%SOURCE_RES%~)      = $missileac(~%SOURCE_RES%~) + $missileac(~%usename%~)
            SET $slashres(~%SOURCE_RES%~)       = $slashres(~%SOURCE_RES%~) + $slashres(~%usename%~)
            SET $crushres(~%SOURCE_RES%~)       = $crushres(~%SOURCE_RES%~) + $crushres(~%usename%~)
            SET $pierceres(~%SOURCE_RES%~)      = $pierceres(~%SOURCE_RES%~) + $pierceres(~%usename%~)
            SET $missileres(~%SOURCE_RES%~)     = $missileres(~%SOURCE_RES%~) + $missileres(~%usename%~)
            SET $fireres(~%SOURCE_RES%~)        = $fireres(~%SOURCE_RES%~) + $fireres(~%usename%~)
            SET $coldres(~%SOURCE_RES%~)        = $coldres(~%SOURCE_RES%~) + $coldres(~%usename%~)
            SET $hidemod(~%SOURCE_RES%~)        = $hidemod(~%SOURCE_RES%~) + $hidemod(~%usename%~)
            SET $movesilmod(~%SOURCE_RES%~)     = $movesilmod(~%SOURCE_RES%~) + $movesilmod(~%usename%~)
            SET $openlockmod(~%SOURCE_RES%~)    = $openlockmod(~%SOURCE_RES%~) + $openlockmod(~%usename%~)
            SET $disarmtrapmod(~%SOURCE_RES%~)  = $disarmtrapmod(~%SOURCE_RES%~) + $disarmtrapmod(~%usename%~)
            SET $pickpocketmod(~%SOURCE_RES%~)  = $pickpocketmod(~%SOURCE_RES%~) + $pickpocketmod(~%usename%~)
            SET $spellfailmod(~%SOURCE_RES%~)   = $spellfailmod(~%SOURCE_RES%~) + $spellfailmod(~%usename%~)
            SPRINT usename ~%SOURCE_RES%~
        END
    END

    PATCH_IF NOT ( ~%usename%~ STRING_EQUAL ~~ ) BEGIN
        PATCH_IF ($weight(~%usename%~) > 0) BEGIN
            WRITE_LONG 0x4c $weight(~%usename%~)
        END
    END

    PATCH_IF NOT ( ~%usename%~ STRING_EQUAL ~~ ) BEGIN
        /* We have something to do, yay */
        READ_SHORT 0x70 fxnum
        READ_LONG 0x6a fxoff
        
        FOR (x=0; (x < %fxnum%); x+=1) BEGIN
            READ_SHORT %fxoff% + 0x30 * %x% + 0     opcode
            READ_LONG  %fxoff% + 0x30 * %x% + 0x04  param1
            READ_LONG  %fxoff% + 0x30 * %x% + 0x08  param2
            
            PATCH_IF (
                (%opcode% = 0) ||                                       // any AC effects
                (%opcode% = 145 && %param2% = 0) ||                     // wizard spell disable
                (%opcode% = 144 && (%param2% = 0 || %param2% = 1)) ||   // stealth/thieving disable
                (%opcode% = 59 && %param1% < 0) ||                      // move silently penalty (but keep bonuses)
                (%opcode% = 275 && %param1% < 0) ||                     // hide in shadows penalty
                (%opcode% = 90 && %param1% < 0) ||                      // open lock penalty
                (%opcode% = 91 && %param1% < 0) ||                      // find trap penalty
                (%opcode% = 92 && %param1% < 0) ||                      // pickpocket penalty
                (%opcode% = 126 && %param1% < 0 && %param2% = 0)        // movement slowdown
                  ) BEGIN
                PATCH_INCLUDE ~delitmfx.tpp~                            // kill them. we'll add some back.
            END
        END
        
        WRITE_SHORT 0x70 %fxnum%
        
        PATCH_FOR_EACH param IN ~dexmod~ ~movemod~ ~wpnspeedmod~ ~baseac~ ~slashac~ ~crushac~ ~pierceac~ ~missileac~
                                ~slashres~ ~crushres~ ~pierceres~ ~missileres~ ~fireres~ ~coldres~
                                ~hidemod~ ~movesilmod~ ~openlockmod~ ~disarmtrapmod~ ~pickpocketmod~
                                ~spellfailmod~
        BEGIN
            SET x = EVALUATE_BUFFER ~%%param%_%usename%%~
            PATCH_IF NOT (x = 0 || (x = 10 && ~%param%~ STRING_EQUAL ~baseac~)) BEGIN 
                PATCH_IF ~%param%~ STRING_EQUAL dexmod BEGIN
                    SET opcode = 15
                    SET param1 = 0 - x
                    SET param2 = 0
                    PATCH_INCLUDE ~additmfx.tpp~ END
                PATCH_IF ~%param%~ STRING_EQUAL movemod BEGIN
                    SET opcode = 176
                    SET param1 = 0 - (x / 10)
                    SET param2 = 0
                    PATCH_INCLUDE ~additmfx.tpp~ END
                PATCH_IF ~%param%~ STRING_EQUAL wpnspeedmod BEGIN
                    SET opcode = 190
                    SET param1 = 0 - x
                    SET param2 = 0
                    PATCH_INCLUDE ~additmfx.tpp~ END
                PATCH_IF ~%param%~ STRING_EQUAL baseac BEGIN
                    SET opcode = 0
                    SET param1 = x
                    SET param2 = 16
                    PATCH_INCLUDE ~additmfx.tpp~ END
                PATCH_IF ~%param%~ STRING_EQUAL slashac BEGIN
                    SET opcode = 0
                    SET param1 = x
                    SET param2 = 8
                    PATCH_INCLUDE ~additmfx.tpp~ END
                PATCH_IF ~%param%~ STRING_EQUAL crushac BEGIN
                    SET opcode = 0
                    SET param1 = x
                    SET param2 = 1
                    PATCH_INCLUDE ~additmfx.tpp~ END
                PATCH_IF ~%param%~ STRING_EQUAL pierceac BEGIN
                    SET opcode = 0
                    SET param1 = x
                    SET param2 = 4
                    PATCH_INCLUDE ~additmfx.tpp~ END
                PATCH_IF ~%param%~ STRING_EQUAL missileac BEGIN
                    SET opcode = 0
                    SET param1 = x
                    SET param2 = 2
                    PATCH_INCLUDE ~additmfx.tpp~ END
                PATCH_IF ~%param%~ STRING_EQUAL slashres BEGIN
                    SET opcode = 86
                    SET param1 = x
                    SET param2 = 0
                    PATCH_INCLUDE ~additmfx.tpp~ END
                PATCH_IF ~%param%~ STRING_EQUAL crushres BEGIN
                    SET opcode = 87
                    SET param1 = x
                    SET param2 = 0
                    PATCH_INCLUDE ~additmfx.tpp~ END
                PATCH_IF ~%param%~ STRING_EQUAL pierceres BEGIN
                    SET opcode = 88
                    SET param1 = x
                    SET param2 = 0
                    PATCH_INCLUDE ~additmfx.tpp~ END
                PATCH_IF ~%param%~ STRING_EQUAL missileres BEGIN
                    SET opcode = 89
                    SET param1 = x
                    SET param2 = 0
                    PATCH_INCLUDE ~additmfx.tpp~ END
                PATCH_IF ~%param%~ STRING_EQUAL fireres BEGIN
                    SET opcode = 30
                    SET param1 = x
                    SET param2 = 0
                    PATCH_INCLUDE ~additmfx.tpp~ END
                PATCH_IF ~%param%~ STRING_EQUAL coldres BEGIN
                    SET opcode = 28
                    SET param1 = x
                    SET param2 = 0
                    PATCH_INCLUDE ~additmfx.tpp~ END
                PATCH_IF ~%param%~ STRING_EQUAL hidemod BEGIN
                    SET opcode = 275
                    SET param1 = 0 - x
                    SET param2 = 0
                    PATCH_INCLUDE ~additmfx.tpp~ END
                PATCH_IF ~%param%~ STRING_EQUAL movesilmod BEGIN
                    SET opcode = 59
                    SET param1 = 0 - x
                    SET param2 = 0
                    PATCH_INCLUDE ~additmfx.tpp~ END
                PATCH_IF ~%param%~ STRING_EQUAL openlockmod BEGIN
                    SET opcode = 90
                    SET param1 = 0 - x
                    SET param2 = 0
                    PATCH_INCLUDE ~additmfx.tpp~ END
                PATCH_IF ~%param%~ STRING_EQUAL disarmtrapmod BEGIN
                    SET opcode = 91
                    SET param1 = 0 - x
                    SET param2 = 0
                    PATCH_INCLUDE ~additmfx.tpp~ END
                PATCH_IF ~%param%~ STRING_EQUAL pickpocketmod BEGIN
                    SET opcode = 92
                    SET param1 = 0 - x
                    SET param2 = 0
                    PATCH_INCLUDE ~additmfx.tpp~ END
                PATCH_IF ~%param%~ STRING_EQUAL spellfailmod BEGIN
                    SET opcode = 60
                    SET param1 = x
                    SET param2 = 0
                    PATCH_INCLUDE ~additmfx.tpp~ END
            END
        END

        WRITE_SHORT 0x70 %fxnum%

    END
    /* Description updates */
    PATCH_IF NOT ( ~%usename%~ STRING_EQUAL ~~ ) BEGIN
        READ_STRREF 0x50 uniddesc ELSE ~nope~
        READ_STRREF 0x54 iddesc ELSE ~nope~
        
        SET resistanceheaderprinted = 0
        SET slowheaderprinted = 0

        PATCH_FOR_EACH param IN ~weight~ ~dexmod~ ~movemod~ ~wpnspeedmod~ ~baseac~ ~slashac~ ~crushac~ ~pierceac~ ~missileac~
                                ~slashres~ ~crushres~ ~pierceres~ ~missileres~ ~fireres~ ~coldres~
                                ~spellfailmod~ ~pickpocketmod~ ~disarmtrapmod~ ~openlockmod~ ~movesilmod~ ~hidemod~ 
        BEGIN

             SET x = EVALUATE_BUFFER ~%%param%_%usename%%~
             PATCH_IF NOT (x = 0 || (x = 10 && ~%param%~ STRING_EQUAL ~baseac~)) BEGIN 
                 PATCH_IF ~%param%~ STRING_EQUAL weight BEGIN
                     SPRINT searchstring   ~Weight:~
                     SPRINT insertafter    ~\(STATISTICS.*[%wnl%%lnl%%mnl%].*[%wnl%%lnl%%mnl%].*[%wnl%%lnl%%mnl%]\)~
                     SPRINT replacestring  ~Weight:[%tab% ]*[0-9]+[%tab% a-zA-Z.]*~
                     SPRINT insertstring   ~Weight: %x%~
                     PATCH_INCLUDE ~descupd_magic.tpp~ END
                
                 PATCH_IF ~%param%~ STRING_EQUAL baseac BEGIN // kill "AC:" lines
                     SPRINT searchstring   ~AC:~
                     SPRINT insertafter    ~\(STATISTICS\)~
                     SPRINT replacestring  ~AC:[%tab% ]*-*[0-9]+[%tab% a-zA-Z0-9+.()]*[%wnl%%lnl%%mnl%]+~
                     SPRINT insertstring   ~~
                     PATCH_INCLUDE ~descupd_magic.tpp~ END

                 PATCH_IF ~%param%~ STRING_EQUAL baseac BEGIN
                     SPRINT searchstring   ~Armor Class:~
                     SPRINT insertafter    ~\(STATISTICS.*[%wnl%%lnl%%mnl%].*[%wnl%%lnl%%mnl%]\)~
                     SPRINT replacestring  ~Armor Class:[%tab% ]*-*[0-9]+[%tab% a-zA-Z0-9+.()]*~
                     SPRINT insertstring   ~Armor Class: %x%~
                     PATCH_INCLUDE ~descupd_magic.tpp~ END

                 PATCH_IF ((~%param%~ STRING_EQUAL slashres || ~%param%~ STRING_EQUAL crushres ||
                            ~%param%~ STRING_EQUAL pierceres || ~%param%~ STRING_EQUAL missileres)
                            && resistanceheaderprinted = 0) BEGIN
                     SPRINT searchstring   ~THISWILLNEVERMATCH~
                     SPRINT insertafter    ~\(STATISTICS.*[%wnl%%lnl%%mnl%].*[%wnl%%lnl%%mnl%]\)~
                     SPRINT replacestring  ~~
                     SPRINT insertstring   ~Protection:%wnl% vs. Slash/Crush/Pierce/Missile:%wnl%  0 / 0 / 0 / 0~
                     SET resistanceheaderprinted = 1
                     SET slashrestotal = 0
                     SET crushrestotal = 0
                     SET piercerestotal = 0
                     SET missilerestotal = 0
                     PATCH_INCLUDE ~descupd_magic.tpp~ END

                 PATCH_IF ~%param%~ STRING_EQUAL slashres BEGIN
                     SET intopcode = 86
                     SET intparam = 1
                     PATCH_INCLUDE ~sumfx.tpp~
                     SET slashrestotal = %returnval%
                     END

                 PATCH_IF ~%param%~ STRING_EQUAL crushres BEGIN
                     SET intopcode = 87
                     SET intparam = 1
                     PATCH_INCLUDE ~sumfx.tpp~
                     SET crushrestotal = %returnval%
                     END

                 PATCH_IF ~%param%~ STRING_EQUAL pierceres BEGIN
                     SET intopcode = 88
                     SET intparam = 1
                     PATCH_INCLUDE ~sumfx.tpp~
                     SET piercerestotal = %returnval%
                     END

                 PATCH_IF ~%param%~ STRING_EQUAL missileres BEGIN
                     SET intopcode = 89
                     SET intparam = 1
                     PATCH_INCLUDE ~sumfx.tpp~
                     SET missilerestotal = %returnval%
                     END

                 PATCH_IF ((~%param%~ STRING_EQUAL dexmod || ~%param%~ STRING_EQUAL movemod ||
                            ~%param%~ STRING_EQUAL wpnspeedmod)
                            && slowheaderprinted = 0) BEGIN
                     SPRINT searchstring   ~THISWILLNEVERMATCH~
                     SPRINT insertafter    ~\(STATISTICS.*[%wnl%%lnl%%mnl%].*[%wnl%%lnl%%mnl%]\)~
                     SPRINT replacestring  ~~
                     SPRINT insertstring   ~Slowdown:%wnl% Dex/Move Speed/Weapon Speed:%wnl%  0 / 0 / 0 ~
                     SET slowheaderprinted = 1
                     SET dexmodtotal = 0
                     SET movemodtotal = 0
                     SET wpnspeedtotal = 0
                     PATCH_INCLUDE ~descupd_magic.tpp~ END

                 PATCH_IF ~%param%~ STRING_EQUAL dexmod BEGIN // kill various DEX lines
                     SPRINT searchstring   ~DEX bonus~
                     SPRINT insertafter    ~\(STATISTICS\)~
                     SPRINT replacestring  ~+[1-9] DEX bonus, ~
                     SPRINT insertstring   ~~
                     PATCH_INCLUDE ~descupd_magic.tpp~
                     SPRINT searchstring   ~Bonus.*Dexterity~
                     SPRINT insertafter    ~\(STATISTICS\)~
                     SPRINT replacestring  ~Bonus.*Dexterity[%wnl%%lnl%%mnl%]~
                     SPRINT insertstring   ~~
                     PATCH_INCLUDE ~descupd_magic.tpp~
                     SPRINT searchstring   ~.*Dexterity~
                     SPRINT insertafter    ~\(STATISTICS\)~
                     SPRINT replacestring  ~.*Dexterity[%wnl%%lnl%%mnl%]~
                     SPRINT insertstring   ~~
                     PATCH_INCLUDE ~descupd_magic.tpp~
                     SET intopcode = 15
                     SET intparam = 1
                     PATCH_INCLUDE ~sumfx.tpp~
                     SET dexmodtotal = %returnval%
                     END

                 PATCH_IF ~%param%~ STRING_EQUAL movemod BEGIN
                     SET intopcode = 176
                     SET intparam = 1
                     PATCH_INCLUDE ~sumfx.tpp~
                     SET movemodtotal = %returnval% * 10
                     END

                 PATCH_IF ~%param%~ STRING_EQUAL wpnspeedmod BEGIN
                     SET intopcode = 190
                     SET intparam = 1
                     PATCH_INCLUDE ~sumfx.tpp~
                     SET wpnspeedtotal = %returnval%
                     END

                 PATCH_IF ~%param%~ STRING_EQUAL hidemod BEGIN
                     SET intopcode = 275
                     SET intparam = 1
                     PATCH_INCLUDE ~sumfx.tpp~
                     SET x = %returnval%
                     PATCH_IF (x > 0) BEGIN
                       SPRINT x ~+%x%~
                     END
                     SPRINT searchstring   ~Hide.*Shadows~
                     SPRINT insertafter    ~\(Armor Class:[%tab% ]*-*[0-9]+[%tab% a-zA-Z0-9+.()]*[%wnl%%lnl%%mnl%]+\)~
                     SPRINT replacestring  ~Hide.*Shadows:.*[1-9]*%perc%*~
                     SPRINT insertstring   ~Hide in Shadows: %x%~
                     PATCH_INCLUDE ~descupd_magic.tpp~ END

                 PATCH_IF (~%param%~ STRING_EQUAL movesilmod || ((~%SOURCE_RES%~ STRING_EQUAL ~c2leat01~ ||  // hack to force
                                                                  ~%SOURCE_RES%~ STRING_EQUAL ~leat21~) &&   // description update
                                                                  ~%param%~ STRING_EQUAL weight)) BEGIN      // of Human Flesh
                     SET intopcode = 59
                     SET intparam = 1
                     PATCH_INCLUDE ~sumfx.tpp~
                     SET x = %returnval%
                     PATCH_IF (x > 0) BEGIN
                       SPRINT x ~+%x%~
                     END
                     SPRINT searchstring   ~\(Stealth|Move Silently\)~
                     SPRINT insertafter    ~\(Armor Class:[%tab% ]*-*[0-9]+[%tab% a-zA-Z0-9+.()]*[%wnl%%lnl%%mnl%]+\)~
                     SPRINT replacestring  ~\(Stealth|Move Silently\):.*[1-9]*%perc%*.*~
                     SPRINT insertstring   ~Move Silently: %x%~
                     PATCH_INCLUDE ~descupd_magic.tpp~ END

                 PATCH_IF ~%param%~ STRING_EQUAL openlockmod BEGIN
                     SET intopcode = 90
                     SET intparam = 1
                     PATCH_INCLUDE ~sumfx.tpp~
                     SET x = %returnval%
                     PATCH_IF (x > 0) BEGIN
                       SPRINT x ~+%x%~
                     END
                     SPRINT searchstring   ~Lock~
                     SPRINT insertafter    ~\(Armor Class:[%tab% ]*-*[0-9]+[%tab% a-zA-Z0-9+.()]*[%wnl%%lnl%%mnl%]+\)~
                     SPRINT replacestring  ~\(Open.*ocks|Lock Picking\):.*[1-9]*%perc%*~
                     SPRINT insertstring   ~Open Locks: %x%~
                     PATCH_INCLUDE ~descupd_magic.tpp~ END

                 PATCH_IF ~%param%~ STRING_EQUAL disarmtrapmod BEGIN
                     SET intopcode = 91
                     SET intparam = 1
                     PATCH_INCLUDE ~sumfx.tpp~
                     SET x = %returnval%
                     PATCH_IF (x > 0) BEGIN
                       SPRINT x ~+%x%~
                     END
                     SPRINT searchstring   ~Find/Remove~
                     SPRINT insertafter    ~\(Armor Class:[%tab% ]*-*[0-9]+[%tab% a-zA-Z0-9+.()]*[%wnl%%lnl%%mnl%]+\)~
                     SPRINT replacestring  ~Find/Remove.*raps:.*[1-9]*%perc%*~
                     SPRINT insertstring   ~Find/Remove Traps: %x%~
                     PATCH_INCLUDE ~descupd_magic.tpp~ END

                 PATCH_IF ~%param%~ STRING_EQUAL pickpocketmod BEGIN
                     SET intopcode = 92
                     SET intparam = 1
                     PATCH_INCLUDE ~sumfx.tpp~
                     SET x = %returnval%
                     PATCH_IF (x > 0) BEGIN
                       SPRINT x ~+%x%~
                     END
                     SPRINT searchstring   ~Pick Pockets~
                     SPRINT insertafter    ~\(Armor Class:[%tab% ]*-*[0-9]+[%tab% a-zA-Z0-9+.()]*[%wnl%%lnl%%mnl%]+\)~
                     SPRINT replacestring  ~Pick Pockets:.*[1-9]*%perc%*~
                     SPRINT insertstring   ~Pick Pockets: %x%~
                     PATCH_INCLUDE ~descupd_magic.tpp~ END

                 PATCH_IF ~%param%~ STRING_EQUAL spellfailmod BEGIN
                     SET intopcode = 60
                     SET intparam = 1
                     PATCH_INCLUDE ~sumfx.tpp~
                     SET x = %returnval%
                     PATCH_IF (x > 0) BEGIN
                       SPRINT x ~+%x%~
                     END
                     SPRINT searchstring   ~Spell Failure~
                     SPRINT insertafter    ~\(Armor Class:[%tab% ]*-*[0-9]+[%tab% a-zA-Z0-9+.()]*[%wnl%%lnl%%mnl%]+\)~
                     SPRINT replacestring  ~Spell Failure:.*[1-9]*%perc%*~
                     SPRINT insertstring   ~Mage Spell Failure: %x%%perc%~
                     PATCH_INCLUDE ~descupd_magic.tpp~ END

            END // if the param has changed
        END // foreach

        // batched updates ... where we have multiple values on one line.
        // elaborate tricks with QUOTE, REPLACE_EVALUATE and EVALUATE_BUFFER failed.
        PATCH_IF (resistanceheaderprinted = 1) BEGIN
            SPRINT searchstring   ~ vs. Slash/Crush/Pierce/Missile:~
            SPRINT insertafter    ~THISWILLNEVERMATCH~
            SPRINT replacestring  ~Slash/Crush/Pierce/Missile:%wnl%  0 / 0 / 0 / 0~
            SPRINT insertstring   ~Slash/Crush/Pierce/Missile:%wnl%  %slashrestotal%%perc% / %crushrestotal%%perc% / %piercerestotal%%perc% / %missilerestotal%%perc%~
            PATCH_INCLUDE ~descupd_magic.tpp~
        END
        PATCH_IF (slowheaderprinted = 1) BEGIN
            SPRINT searchstring   ~ Dex/Move Speed/Weapon Speed:~
            SPRINT insertafter    ~THISWILLNEVERMATCH~
            SPRINT replacestring  ~ Dex/Move Speed/Weapon Speed:%wnl%  0 / 0 / 0 ~
            SPRINT insertstring   ~ Dex/Move Speed/Weapon Speed:%wnl%  %dexmodtotal% / %movemodtotal%%perc% / %wpnspeedtotal% ~
            PATCH_INCLUDE ~descupd_magic.tpp~
        END

        // some custom updates for less-than-accurate statements after mod install
        PATCH_IF (~%SOURCE_RES%~ STRING_EQUAL ~c2keld01~) BEGIN
             SPRINT searchstring   ~Despite~
             SPRINT insertafter    ~THISWILLNEVERMATCH~
             SPRINT replacestring  ~ Despite being incredibly strong, the armor is almost weightless.~
             SPRINT insertstring   ~~
             PATCH_INCLUDE ~descupd_magic.tpp~
        END
        PATCH_IF (~%SOURCE_RES%~ STRING_EQUAL ~leat16~) BEGIN // orc leather
             SPRINT searchstring   ~Bonuses:~
             SPRINT insertafter    ~THISWILLNEVERMATCH~
             SPRINT replacestring  ~Bonuses: .* resistance to missile attacks[%wnl%%lnl%%mnl%]+~
             SPRINT insertstring   ~~
             PATCH_INCLUDE ~descupd_magic.tpp~
        END
        PATCH_IF (~%SOURCE_RES%~ STRING_EQUAL ~rr#abih~) BEGIN // abishai hide
             SPRINT searchstring   ~resistance~
             SPRINT insertafter    ~THISWILLNEVERMATCH~
             SPRINT replacestring  ~    .*resistance to slashing, piercing, crushing and missile damage[%wnl%%lnl%%mnl%]+~
             SPRINT insertstring   ~~
             PATCH_INCLUDE ~descupd_magic.tpp~
        END
        PATCH_IF (~%SOURCE_RES%~ STRING_EQUAL ~c2chan01~) BEGIN // incarnadine elven chain - no thieving penalties
             SPRINT searchstring   ~Incarnadine~
             SPRINT insertafter    ~THISWILLNEVERMATCH~
             SPRINT replacestring  ~Stealth:.*[%wnl%%lnl%%mnl%]+~
             SPRINT insertstring   ~~
             PATCH_INCLUDE ~descupd_magic.tpp~
             SPRINT replacestring  ~Lockpicking:.*[%wnl%%lnl%%mnl%]+~
             PATCH_INCLUDE ~descupd_magic.tpp~
             SPRINT replacestring  ~Find/Remove.*[%wnl%%lnl%%mnl%]+~
             PATCH_INCLUDE ~descupd_magic.tpp~
             SPRINT replacestring  ~Pick.*[%wnl%%lnl%%mnl%]+~
             PATCH_INCLUDE ~descupd_magic.tpp~
        END

    END // if not empty usename

  END
  BUT_ONLY_IF_IT_CHANGES

BEGIN ~Full Plate And Packing Steel: Field Improvisation (convenience tweak, remove restrictions on combining protective items)~
 DESIGNATED 102

COPY ~fullplate/itemexcl.2da~ ~override~
